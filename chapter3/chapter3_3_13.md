# 3.3.13 SET

> `SET` 절은 노드의 라벨, 노드나 관계의 속성을 업데이트하는데 사용됩니다.

* [소개](#chapter3313_1)
* [속성 설정](#chapter3313_2)
* [속성 제거](#chapter3313_3)
* [노드와 관계간 속성 복사](#chapter3313_4)
* [맵으로부터 속성 추가](#chapter3313_5)
* [파라미터를 사용한 속성 설정](#chapter3313_6)
* [파라미터를 사용한 모든 속성 설정](#chapter3313_7)
* [하나의 `SET`절에 다중 속성 설정](#chapter3313_8)
* [노드의 라벨 설정](#chapter3313_9)
* [노드에 다중 라벨 설정](#chapter3313_10)

## 3.3.13.1 소개 {#chapter3313_1}

`SET`은 파라미터로부터 속성을 설정하는데 사용됩니다.

> 노드에 라벨을 설정하는 것은 멱등원 연산입니다. - 만약 해당 라벨을 가지고 있는 노드에 중복으로 설정할 경우 아무런 일도 발생하지 않습니다.

아래의 그래프를 가지고 진행하겠습니다.

![](https://neo4j.com/docs/developer-manual/current/images/cypher-set-graph.svg)

## 3.3.13.2 속성 설정 {#chapter3313_2}

노드나 관계에 속성을 설정할 때 `SET`을 사용합니다.

### 쿼리

```cypher
MATCH (n { name: 'Andres' })
SET n.surname = 'Taylor'
RETURN n
```

쿼리에 의해 새롭게 바뀐 노드가 반환됩니다.

### 쿼리결과

```
+------------------------------------------------------------+
| n                                                          |
+------------------------------------------------------------+
| Node[0]{surname:"Taylor",name:"Andres",age:36,hungry:true} |
+------------------------------------------------------------+
1 row
Properties set: 1
```

## 3.3.13.3 속성 제거 {#chapter3313_3}

보통은 `REMOVE`를 사용하여 속성을 제거하지만, 가끔은 `SET`으로 처리가 가능합니다. 한가지 예로 파라미터로부터 오는 경우입니다.

### 쿼리

```cypher
MATCH (n { name: 'Andres' })
SET n.name = NULL RETURN n
```

해당 노드가 반환되지만, name 속성은 제거됩니다.

### 쿼리결과

```
+-----------------------------+
| n                           |
+-----------------------------+
| Node[0]{hungry:true,age:36} |
+-----------------------------+
1 row
Properties set: 1
```

## 3.3.13.4 노드와 관계간 속성 복사 {#chapter3313_4}

`SET`을 활용해서 그래프 요소간의 모든 속성을 복사할 수 있습니다. 참고로 속성을 받는 쪽의 대상은 기존의 모든 다른 속성이 삭제됩니다.

### 쿼리

```cypher
MATCH (at { name: 'Andres' }),(pn { name: 'Peter' })
SET at = pn
RETURN at, pn
```

**Andres** 노드의 가지고 있는 모든 속성이 **Peter** 노드의 속성으로 교체됩니다.

### 쿼리결과

```
+-------------------------------------------------------------+
| at                           | pn                           |
+-------------------------------------------------------------+
| Node[0]{name:"Peter",age:34} | Node[2]{name:"Peter",age:34} |
+-------------------------------------------------------------+
1 row
Properties set: 3
```

## 3.3.13.5 맵으로부터 속성 추가 {#chapter3313_5}

맵(표현식, 파라미터 또는 그래프 요소)으로 속성을 설정할 경우 `SET`절에 `+=`를 사용해서 그래프 요소에 기존 속성을 삭제하지 않고 추가할 수 있습니다.

### 쿼리

```cypher
MATCH (peter { name: 'Peter' })
SET peter += { hungry: TRUE , position: 'Entrepreneur' }
```

### 쿼리결과

```
+-------------------+
| No data returned. |
+-------------------+
Properties set: 2
```

## 3.3.13.6 파라미터를 사용한 속성 설정 {#chapter3313_6}

속성의 값으로 파라미터를 사용할 수 있습니다.

### 파라미터

```json
{
  "surname" : "Taylor"
}
```

### 쿼리

```cypher
MATCH (n { name: 'Andres' })
SET n.surname = $surname
RETURN n
```

**Andres** 노드에 **surname**이 추가됩니다.

### 쿼리결과

```
+------------------------------------------------------------+
| n                                                          |
+------------------------------------------------------------+
| Node[0]{surname:"Taylor",name:"Andres",age:36,hungry:true} |
+------------------------------------------------------------+
1 row
Properties set: 1
```

## 3.3.13.7 파라미터를 사용한 모든 속성 설정 {#chapter3313_7}

파라미터로 전달된 새로운 속성으로 기존의 속성을 모두 교체하는 방법입니다.

### 파라미터

```json
{
  "props" : {
    "name" : "Andres",
    "position" : "Developer"
  }
}
```

### 쿼리

```cypher
MATCH (n { name: 'Andres' })
SET n = $props
RETURN n
```

**Andres** 노드는 기존의 모든 속성이 `props` 파라미터로 전달된 속성으로 교체가 됩니다.

### 쿼리결과

```
+---------------------------------------------+
| n                                           |
+---------------------------------------------+
| Node[0]{name:"Andres",position:"Developer"} |
+---------------------------------------------+
1 row
Properties set: 4
```

## 3.3.13.8 하나의 `SET`절에 다중 속성 설정 {#chapter3313_8}

만약 하나의 쿼리에 다수의 속성을 설정할 경우에는 쉼표를 활용하여 구분하면 됩니다.

### 쿼리

```cypher
MATCH (n { name: 'Andres' })
SET n.position = 'Developer', n.surname = 'Taylor'
```

### 쿼리결과

```
+-------------------+
| No data returned. |
+-------------------+
Properties set: 2
```

## 3.3.13.9 노드의 라벨 설정 {#chapter3313_9}

노드에 라벨을 설정할 경우 `SET`을 활용하면 됩니다.

### 쿼리

```cypher
MATCH (n { name: 'Stefan' })
SET n:German
RETURN n
```

새로운 라벨이 추가된 노드가 반환됩니다.

### 쿼리결과

```
+------------------------+
| n                      |
+------------------------+
| Node[3]{name:"Stefan"} |
+------------------------+
1 row
Labels added: 1
```

## 3.3.13.10 노드에 다중 라벨 설정 {#chapter3313_10}

노드에 다수의 라벨을 설정할 경우 `SET` 절에 `:`를 사용하여 다른 라벨을 지정하면 됩니다.

### 쿼리

```cypher
MATCH (n { name: 'Emil' })
SET n:Swedish:Bossman
RETURN n
```

새로운 라벨이 추가된 노드가 반환됩니다.

### 쿼리결과

```
+----------------------+
| n                    |
+----------------------+
| Node[1]{name:"Emil"} |
+----------------------+
1 row
Labels added: 2
```